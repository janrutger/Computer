# ============================================================================
# Multi layer perceptron test code
# ============================================================================

USE std_heap
USE std_stern_io
USE std_time
USE std_stacks_rt

INCLUDE math_lib
INCLUDE fixed_point_lib
INCLUDE gpu3_lib

# INCLUDE mlnn_lib
INCLUDE mlnn_gpu3_optimized_lib

VALUE network_ptr 0
VALUE output_array_ptr 0
VALUE current_input_ptr 0
VALUE current_target_ptr 0
VALUE learning_rate 0

VALUE epochs 0
VALUE i 0
VALUE rand_idx 0
VALUE xor_inputs_ptr 0
VALUE xor_targets_ptr 0

STRING fp_0_0 "0.0"
STRING fp_0_1 "0.1"
STRING fp_0_5 "0.5"
STRING fp_1_0 "1.0"
STRING fp_0_01 "0.01"



DEF SRAND {     # uses std_stacks_rt to acces rand_m var from RND
    *p_currentime rand_m % AS random_seed
    "New random seed: " PRTstring
    random_seed PRINT
}



# ============================================================================
# Main Program
# ============================================================================

# --- 1. Initialization ---
# HEAP_MEM_START HEAP_MEM_SIZE HEAP.init
HEAP.free
SRAND    

# Initialize fixed-point library with 4 decimal places of precision
10000 FP.set_scale
10000 NN.set_scale

"--- Phase 1: Creating a 2-4-1 network...\n" PRTstring
#"Creating a 2-4-1 network...\n" PRTstring

2 4 1 NN.new_network AS network_ptr

"Network created at address: " PRTstring
network_ptr PRINT


"\n--- Phase 2: Training the Network ---\n" PRTstring
# First, let's create the XOR dataset.
"Creating XOR dataset...\n" PRTstring

# Create an array to hold pointers to the 4 input arrays
4 NEW.array AS xor_inputs_ptr

# Input [0, 0]
2 NEW.array AS current_input_ptr
&fp_0_0 FP.from_string current_input_ptr ARRAY.append
&fp_0_0 FP.from_string current_input_ptr ARRAY.append
current_input_ptr xor_inputs_ptr ARRAY.append

# Input [0, 1]
2 NEW.array AS current_input_ptr
&fp_0_0 FP.from_string current_input_ptr ARRAY.append
&fp_1_0 FP.from_string current_input_ptr ARRAY.append
current_input_ptr xor_inputs_ptr ARRAY.append

# Input [1, 0]
2 NEW.array AS current_input_ptr
&fp_1_0 FP.from_string current_input_ptr ARRAY.append
&fp_0_0 FP.from_string current_input_ptr ARRAY.append
current_input_ptr xor_inputs_ptr ARRAY.append

# Input [1, 1]
2 NEW.array AS current_input_ptr
&fp_1_0 FP.from_string current_input_ptr ARRAY.append
&fp_1_0 FP.from_string current_input_ptr ARRAY.append
current_input_ptr xor_inputs_ptr ARRAY.append

# Create an array for the target outputs: [0, 1, 1, 0]
4 NEW.array AS xor_targets_ptr
&fp_0_0 FP.from_string xor_targets_ptr ARRAY.append
&fp_1_0 FP.from_string xor_targets_ptr ARRAY.append
&fp_1_0 FP.from_string xor_targets_ptr ARRAY.append
&fp_0_0 FP.from_string xor_targets_ptr ARRAY.append

# Create a single, reusable target array for the training loop
1 NEW.array AS current_target_ptr

# --- Training Loop ---
1500 AS epochs
&fp_0_1 FP.from_string AS learning_rate # 0.1 in fixed-point
0 AS i

0 TIME.start
"Starting training for " PRTstring epochs PRTnum " epochs...\n" PRTstring

WHILE i epochs < DO
    # Randomly pick a training example
    RND 4 % AS rand_idx

    i 100 % 0 == IF
        i PRTnum 32 PRTchar 0 TIME.read TIME.as_string "\n" PRTstring
    END

    # Clear the reusable target array and get the new input/target for this training step
    current_target_ptr ARRAY.clear
    # xor_inputs_ptr rand_idx ARRAY.get AS current_input_ptr
    # xor_targets_ptr rand_idx ARRAY.get current_target_ptr ARRAY.append
    rand_idx xor_inputs_ptr  ARRAY.get AS current_input_ptr
    rand_idx xor_targets_ptr ARRAY.get current_target_ptr ARRAY.append

    # Perform one step of training
    network_ptr current_input_ptr current_target_ptr learning_rate NN.train

    i 1 + AS i
DONE

"Training complete. \n" PRTstring
"\n--- Phase 3: Testing trained network...\n" PRTstring
0 AS i
WHILE i 4 < DO
    "  Input " PRTstring i PRTnum " -> Prediction: " PRTstring
    # xor_inputs_ptr i ARRAY.get AS current_input_ptr
    i xor_inputs_ptr ARRAY.get AS current_input_ptr

    # network_ptr current_input_ptr NN.predict 0 ARRAY.get 4 FP.fprint "\n" PRTstring
    0 network_ptr current_input_ptr NN.predict  ARRAY.get 4 FP.fprint "\n" PRTstring

    i 1 + AS i
DONE

"\nALL DONE... \n" PRTstring